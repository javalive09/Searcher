apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    signingConfigs {
        //加载资源
        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
        properties.load(inputStream)
        def signKeyAlias = properties.getProperty('keyAlias')
        def signKeyPassword = properties.getProperty('keyPassword')
        def signStoreFileUrl = properties.getProperty('storeFileUrl')
        def signStorePassword = properties.getProperty('storePassword')
        release {
            keyAlias signKeyAlias
            keyPassword signKeyPassword
            storeFile file(signStoreFileUrl)
            storePassword signStorePassword
        }
    }
    defaultConfig {
        applicationId "peter.util.searcher"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 91
        versionName "1.7.9.7"
    }

    buildTypes {
        release {
            //指定签名为release
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        _200code {}
        xiaomi {}
        qihu360 {}
        baidu {}
        wandoujia {}
        tencent {}
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    flavorDimensions "default"

    greendao {
        schemaVersion 1
        daoPackage 'peter.util.searcher.db.dao'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

task release200CodeApk(dependsOn: 'assemble_200code') doLast {
    println 'releaseApk start >>>>>>>>>>>>>>>>>' + buildDir
    def outputFileApkFile = null
    def outputFileApkName = null
    def outputFileApkDir = null
    def finalApkName = 'searcher_' + android.defaultConfig.versionCode + '.apk'
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile.getPath().contains('release') && output.outputFile.getPath().contains('200code')) {
                println "Found output 3file == " + output.outputFile.getPath()
                outputFileApkFile = output.outputFile.getPath()
                outputFileApkName = output.outputFile.name
                outputFileApkDir = output.outputFile.parent
            }
        }
    }
    println "Found output apk: " + outputFileApkFile
    println "Found output name: " + outputFileApkName
    println "Found output dir: " + outputFileApkDir
    copy {
        from(outputFileApkFile)
        into(outputFileApkDir)
        rename(outputFileApkName, finalApkName)
    }
    //cmd upload apk file
    def uploadApkFileName = outputFileApkDir + '/' + finalApkName
    println "Found uploadApkFileName dir: " + uploadApkFileName
    def uploadAPKCMD = '/usr/local/bin/coscmd upload ' + uploadApkFileName + ' ./'
    def uploadAPKResult = uploadAPKCMD.execute().text.trim()
    print uploadAPKResult
    //cmd upload config
    def code = android.defaultConfig.versionCode
    def url = "http://searcher-1254131086.file.myqcloud.com/" + finalApkName
    def num = android.defaultConfig.versionName
    def resultConfig = exec {
        executable 'python'
        args 'config.py', code, url, num
    }
    print uploadAPKResult
    print resultConfig

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.umeng.analytics:analytics:6.1.2'
    implementation 'com.anthonycr.grant:permissions:1.1.2'
    implementation 'com.android.support:design:25.4.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.android.support:cardview-v7:25.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    //chrome debug sqLite chrome://inspect/#devices
    implementation 'com.facebook.stetho:stetho:1.3.1'
    implementation 'com.javalive09.codebag:codebag:1.3.7'
    //chrome debug okHttp
    implementation 'com.facebook.stetho:stetho-okhttp3:1.3.1'
}

